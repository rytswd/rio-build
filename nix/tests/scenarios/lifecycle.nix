# Lifecycle scenario: scheduler recovery, GC, ephemeral pools,
# health-shared NOT_SERVING probe — all exercised against the k3s-full
# fixture.
#
# Ports phase3b sections S (recovery), C (GC), T (health-shared) onto the
# 2-node k3s Helm-chart fixture. Unlike phase3b (control/worker/k8s/client
# as separate systemd VMs), everything here runs as PODS — closes the
# "production uses pod path, VM tests use systemd" gap for the
# reconciler/lease surface.
#
#
# Fragment architecture: this file returns { fragments, mkTest } instead
# of a single runNixOSTest. default.nix composes fragments into parallel
# VM tests — critical path ~8min vs the prior ~14min monolith. Each
# fragment is a Python `with subtest(...)` block; mkTest concatenates a
# prelude + the selected fragments + coverage epilogue into a testScript.
# Key adaptation: scheduler pods are minimal images (no shell, no curl).
# Metric scrapes go through apiserver pods/proxy (`kubectl get --raw`);
# grpcurl (needs raw TCP) through `kubectl port-forward`.
# Scheduler has 2 replicas (podAntiAffinity spreads them
# across server+agent), so killing the leader means the STANDBY takes
# over — a strictly stronger recovery test than phase3b's single-instance
# restart.
#
# ctrl.probe.named-service — verify marker at default.nix:subtests[health-shared]
#   health-shared probes with `-service rio.scheduler.SchedulerService`
#   (the named service, NOT empty-string) and asserts NOT_SERVING on
#   standby. scheduler/main.rs (r[impl ctrl.probe.named-service]):
#   set_not_serving only affects the named service. This proves the
#   CLIENT-SIDE BALANCER constraint via grpc-health-probe CLI — NOT
#   the K8s readinessProbe (which is tcpSocket, doesn't probe gRPC
#   health at all).
#
# worker.cancel.cgroup-kill — verify marker at default.nix:subtests[cancel-cgroup-kill]
#   cancel-cgroup-kill calls CancelBuild via gRPC mid-exec and asserts
#   the cgroup is rmdir'd before the sleep completes. cgroup.rs:180
#   kill() writes "1" to cgroup.kill → kernel SIGKILLs the tree. No
#   other test cancels a RUNNING build (recovery kills the scheduler,
#   build keeps running on the worker).
#
# worker.cgroup.kill-on-teardown — verify marker at default.nix:subtests[build-timeout]
# worker.timeout.no-reassign — verify marker at default.nix:subtests[build-timeout]
#   build-timeout submits via gRPC SubmitBuild with buildTimeout=45 against
#   a 90s sleep. The timeout fires mid-build → run_daemon_build returns
#   → executor/mod.rs:764 build_cgroup.kill() fires unconditionally →
#   drain → Drop rmdirs. Asserts cgroup GONE (kernel rejects rmdir on
#   non-empty, so gone ⇒ builder killed ⇒ kill-on-teardown ran) + a
#   second build of the SAME drv succeeds (no EEXIST — leak is closed).
#   Distinct from cancel-cgroup-kill: that tests runtime.rs's explicit
#   cancel-abort path (try_cancel_build, stream-era CancelSignal's
#   surviving machinery); this tests the executor's post-daemon teardown.
#
# worker.upload.references-scanned — verify marker at default.nix:subtests[refs-end-to-end]
#   refs-end-to-end builds a consumer derivation whose $out embeds a
#   dep's store path, then asserts PG narinfo."references" contains
#   that path. Proves RefScanSink → PutPath → PG end-to-end (not just
#   unit-level scanner correctness).
#
# worker.upload.deriver-populated — verify marker at default.nix:subtests[refs-end-to-end]
#   refs-end-to-end asserts narinfo.deriver is the consumer's .drv path
#   (name-matched + .drv suffix). Before the phase4a fix, deriver was
#   always empty — upload.rs never plumbed it from the executor.
#
# store.gc.two-phase — verify marker at default.nix:subtests[refs-end-to-end]
#   refs-end-to-end pins ONLY the consumer, backdates both paths past
#   grace, sweeps, and asserts the dep SURVIVES. Proves mark's recursive
#   CTE actually walks narinfo."references" — if it didn't, dep would
#   be unreachable (no pin, no inbound edge in the CTE) and swept. This
#   is the ONLY VM-level test of mark-follows-refs; gc-sweep's victim
#   has refs=[] by construction (mkTrivial output embeds no store paths).
#
# store.gc.tenant-retention — verify marker at default.nix:subtests[gc-sweep]
#   gc-sweep tail: backdates out_tenant's narinfo past global grace but
#   leaves path_tenants.first_referenced_at inside the tenant's 168h
#   retention window → sweep collects 0 (seed f protects it). Then
#   backdates first_referenced_at past retention too → sweep collects 1.
#   Proves tenant retention EXTENDS global grace (the spec's "floor"
#   semantics) end-to-end with completion-hook-produced rows.
#
# ctrl.pool.ephemeral — verify marker at default.nix:subtests[ephemeral-pool]
#   ephemeral-pool: applies an ephemeral kind=Builder Pool; asserts
#   status.desiredReplicas == replicas.max (reconcile_ephemeral ran and
#   patched status, ephemeral.rs:220-228) and the Job-spawn-on-queue path
#   end-to-end. Subtest deletes the default x86-64 Pool first
#   so its child pool's reconciler doesn't steal dispatch.
{
  pkgs,
  common,
  fixture,
}:
let
  inherit (fixture)
    ns
    nsStore
    nsBuilders
    nsFetchers
    ;
  drvs = import ../lib/derivations.nix { inherit pkgs; };
  protoset = import ../lib/protoset.nix { inherit pkgs; };
  jwtKeys = import ../lib/jwt-keys.nix;

  # grpcurl not in k3sBase systemPackages (only curl+kubectl). Use the
  # store path directly — it's pulled into the VM closure by interpolation.
  grpcurl = "${pkgs.grpcurl}/bin/grpcurl";

  # Mint a tenant JWT signed with the lib/jwt-keys.nix test seed (same
  # seed the k3s-full fixture passes to the chart via jwt.signingSeed).
  # Prelude calls this once after creating the vm-lifecycle tenant; the
  # resulting token is attached to every grpcurl-direct SchedulerService
  # call so require_tenant() (r[sched.tenant.authz]) accepts it. When
  # the fixture has jwtEnabled=false (prod-parity), the scheduler's
  # interceptor is inert and the header is ignored — harmless.
  pyWithJwt = pkgs.python3.withPackages (
    ps: with ps; [
      pyjwt
      cryptography
    ]
  );
  signJwt = pkgs.writeScript "sign-jwt-lifecycle" ''
    #!${pyWithJwt}/bin/python3
    import sys, time, base64, jwt
    from cryptography.hazmat.primitives.asymmetric.ed25519 import Ed25519PrivateKey
    seed = base64.b64decode("${jwtKeys.seedB64}")
    sk = Ed25519PrivateKey.from_private_bytes(seed)
    now = int(time.time())
    claims = {"sub": sys.argv[1], "iat": now, "exp": now + 3600,
              "jti": "vm-lifecycle"}
    print(jwt.encode(claims, sk, algorithm="EdDSA"))
  '';

  # Mint an x-rio-service-token (rio_auth::hmac::ServiceClaims) for
  # AdminService grpcurl calls. G10 gated CreateTenant/TriggerGC/etc.
  # on ensure_service_caller(); the test acts as rio-cli would. The
  # key is read from the LIVE rio-service-hmac Secret (base64 on
  # stdin) — NOT from fixture.hmacKeys: hmac-keys.nix is deterministic
  # now, but signing with the bytes the cluster actually mounted can
  # never diverge from what the scheduler verifies even if the fixture
  # and the deployed Secret drift apart again (the failure class in
  # ci-failure-patterns.md "IFD × non-determinism"). Format matches
  # HmacSigner::sign: base64url_nopad(json) "." base64url_nopad(tag).
  # The decoded key is byte-trimmed for trailing CRLF/LF — mirrors
  # rio_auth::hmac::load_key (the verifier's loader); hmac-keys.nix
  # appends LF as a tripwire so a non-trimming signer fails CI.
  signServiceToken = pkgs.writeScript "sign-service-token-lifecycle" ''
    #!${pkgs.python3}/bin/python3
    import base64, hashlib, hmac, json, sys, time
    key = base64.b64decode(sys.stdin.read().strip())
    for suf in (b"\r\n", b"\n"):
        if key.endswith(suf):
            key = key[: -len(suf)]
            break
    claims = json.dumps(
        {"caller": "rio-cli", "expiry_unix": int(time.time()) + 3600},
        separators=(",", ":"),
    ).encode()
    tag = hmac.new(key, claims, hashlib.sha256).digest()
    b64 = lambda b: base64.urlsafe_b64encode(b).rstrip(b"=").decode()
    print(f"{b64(claims)}.{b64(tag)}")
  '';

  # rio-* gRPC is plaintext-on-WireGuard; grpcurl needs -plaintext.
  grpcurlTls = "-plaintext";

  # ── Test derivations ────────────────────────────────────────────────
  # Distinct markers so each build creates a fresh derivations row —
  # otherwise DAG-dedup would reuse an earlier build's result and the
  # "not a cache hit" assertions would be hollow.

  # Pin target for GC-sweep. Built FIRST (before recovery) so it's been
  # in PG long enough that gc-sweep's backdate targets a DIFFERENT row.
  pinDrv = drvs.mkTrivial { marker = "lifecycle-pin"; };

  # In-flight build for recovery. 60s sleep survives the leader-kill
  # window: steal threshold (STEAL_AFTER=19s + one 5s poll worst case for
  # standby to detect) + standby's recovery query (~1s) + re-dispatch
  # latency (~5s). phase3b rationale
  # (phase3b.nix:85-99) applies verbatim — a shorter sleep lets the build
  # finish during the failover gap → PG has 0 non-terminal rows →
  # recovery loads nothing → hollow test.
  recoverySlowDrv = drvs.mkTrivial {
    marker = "lifecycle-recovery-slow";
    sleepSecs = 60;
  };

  # Post-recovery build. DIFFERENT marker than pinDrv so this is NOT a
  # cache hit — proves dispatch actually unblocked after LeaderAcquired →
  # recover_from_pg → recovery completion recorded for the acquire-epoch
  # (LeaderState::set_recovery_complete). Also becomes the
  # backdate target for gc-sweep (unpinned, so sweep can delete it).
  recoveryDrv = drvs.mkTrivial { marker = "lifecycle-recovery"; };

  # cancel-cgroup-kill in-flight build. 180s sleep: wait-for-running
  # + find cgroup + assert it has procs + ~20s of open-attempt
  # observation evidence + CancelBuild + the AD5 composite cancel
  # bound (90s). The sleep must comfortably exceed observation + the
  # bound so "cgroup gone inside the bound" can only mean the cancel
  # chain killed the build, never that the sleep finished on its own
  # (the pre-pull 60s sleeper left no such margin once the controller
  # Job-deletion hop replaced the stream cancel dispatch).
  cancelDrv = drvs.mkTrivial {
    marker = "lifecycle-cancel";
    sleepSecs = 180;
  };

  # build-timeout victim. sleepSecs=90 vs buildTimeout=45 — wide gap so
  # neither TCG dispatch lag (timeout may fire at 8-12s wall) nor the
  # scheduler's 10s tick granularity lets the sleep finish first. Same
  # marker-in-drvname pattern so the cgroup dir is findable from the
  # VM host (sanitize_build_id: ".drv" → "_drv").
  timeoutDrv = drvs.mkTrivial {
    marker = "lifecycle-timeout";
    sleepSecs = 90;
  };

  # gc-sweep's backdate+delete target. In the monolith, gc-sweep reused
  # `out_recovery` from the recovery subtest — convenient but coupled.
  # Fragment architecture: gc-sweep builds its own victim.
  gcVictimDrv = drvs.mkTrivial { marker = "lifecycle-gc-victim"; };

  # authz-matrix fragment: fresh unique drv so the build runs on a
  # worker (logs + ownership rows written) rather than substituting.
  authzDrv = drvs.mkTrivial { marker = "lifecycle-authz"; };

  # ephemeral-pool: two builds with DISTINCT markers. Same DAG-dedup
  # reasoning as pinDrv/recoveryDrv — the second build must be a fresh
  # derivation, not a cache hit, so reconcile_ephemeral's ClusterStatus
  # poll sees queued > 0 again and spawns a SECOND Job.
  ephemeralDrv1 = drvs.mkTrivial { marker = "lifecycle-ephemeral-1"; };
  ephemeralDrv2 = drvs.mkTrivial { marker = "lifecycle-ephemeral-2"; };

  # pull-mode: the additive PullAssignment/ReportOutcome path.
  # pullDrv1 (30s sleep): long enough that the open attempt is
  # observable in ListOpenAttempts / the open-attempts gauge while the
  # build runs, short enough to keep the subtest budget tight.
  # pullDrv2 (45s sleep): the killed-mid-build arm — the window covers
  # pod start + pull + the force-kill, and the requeued re-attempt
  # pays the same sleep again before the client gets its store path.
  pullDrv1 = drvs.mkTrivial {
    marker = "lifecycle-pull-mode-1";
    sleepSecs = 30;
  };
  pullDrv2 = drvs.mkTrivial {
    marker = "lifecycle-pull-mode-2";
    sleepSecs = 45;
  };

  # pull-canary (vm-pull-canary-k3s only): the scripted pull-pool
  # {success, failure} sequence plus the cancel/preempt/establishment
  # arms. Distinct pname per drv keeps every one unfitted for the SLA
  # estimator (per-pname estimates), so each Job's
  # activeDeadlineSeconds stays at the overlay's 180s probe-deadline
  # floor and the establishment window stays ~300s. The 60s sleepers
  # leave a comfortable margin under the overlay's ~90s worker timeout
  # while staying observable mid-build. (The stream-baseline copies of
  # the ok/fail pair retired with the stream session machinery — 1c'
  # deletion commit A.)
  pcPullOk = drvs.mkTrivial {
    marker = "pc-pull-ok";
    sleepSecs = 5;
  };
  pcPullFail = drvs.mkCustom {
    name = "rio-test-pc-pull-fail";
    script = ''
      ''${busybox}/bin/busybox sleep 5
      ''${busybox}/bin/busybox echo "pull-canary deterministic failure (pull leg)" >&2
      exit 1
    '';
  };
  pcCancelDrv = drvs.mkTrivial {
    marker = "pc-cancel";
    sleepSecs = 60;
  };
  pcPreemptDrv = drvs.mkTrivial {
    marker = "pc-preempt";
    sleepSecs = 60;
  };
  pcEstabDrv = drvs.mkTrivial {
    marker = "pc-estab";
    sleepSecs = 60;
  };

  # pull-fetcher (vm-pull-canary-k3s only): a network-free fixed-output
  # derivation for the fetcher-kind pull arm. FOD-ness (outputHash) is
  # what routes it to the kind=Fetcher pool; the builder just writes a
  # known payload, so the flat sha256 is computable at eval time and the
  # build needs no egress. The 15s sleep keeps the open attempt
  # observable mid-build (same reasoning as pullDrv1).
  pcFetcherFod = pkgs.writeText "drv-pc-fetcher-fod.nix" ''
    { busybox }:
    derivation {
      name = "rio-test-pc-fetcher-fod";
      system = builtins.currentSystem;
      builder = "''${busybox}/bin/sh";
      args = [ "-c" '''
        ''${busybox}/bin/busybox sleep 15
        ''${busybox}/bin/busybox printf '%s' pc-fetcher-payload > $out
      ''' ];
      outputHashMode = "flat";
      outputHashAlgo = "sha256";
      outputHash = builtins.hashString "sha256" "pc-fetcher-payload";
    }
  '';

  # gc-sweep's path_tenants proof. Distinct marker so DAG-dedup doesn't
  # reuse pinDrv/gcVictimDrv (those were built with the empty-comment
  # key → tenant_id=None → completion hook's filter_map drops → upsert
  # never fires). Fresh derivation = fresh build = completion runs.
  tenantDrv = drvs.mkTrivial { marker = "lifecycle-gc-tenant"; };

  # store-rollout: two builds, one before and one after a store
  # Deployment rollout restart. Distinct markers → distinct derivations
  # → each SubmitBuild triggers a fresh FindMissingPaths cache-check
  # against the scheduler's long-held store_client channel.
  rolloutPreDrv = drvs.mkTrivial { marker = "lifecycle-rollout-pre"; };
  rolloutPostDrv = drvs.mkTrivial { marker = "lifecycle-rollout-post"; };

  # fod-substituted-inputs: dep + parent where the parent's only
  # inputDrv is dep. dep is built and Completed BEFORE a scheduler
  # rollout restart, so the post-restart scheduler's recovery (which
  # loads only non-terminal rows) does NOT carry dep in its in-memory
  # DAG. The parent is then submitted; attested_input_seeds must
  # resolve dep's output path through the persisted
  # derivations.expected_output_paths row (the PG-fallback resolver),
  # NOT degrade to None and dispatch with empty input_roots.
  #
  # Parent is a regular build, not an FOD: the resolver is
  # build-kind-agnostic and the recovery split's fixture has no
  # kind=Fetcher pool. The production symptom (1331 stuck FODs) was
  # FOD-shaped because that lane saturated first; the scheduler-side
  # gap this fragment regression-tests is identical for either kind.
  fodSubstitutedDrvFile = pkgs.writeText "lifecycle-fod-substituted.nix" ''
    { busybox }:
    let
      sh = "''${busybox}/bin/sh";
      bb = "''${busybox}/bin/busybox";
      dep = derivation {
        name = "rio-fod-substituted-dep";
        system = builtins.currentSystem;
        builder = sh;
        args = [ "-c" "''${bb} printf '%s' fod-substituted-dep-payload > $out" ];
      };
      parent = derivation {
        name = "rio-fod-substituted-parent";
        system = builtins.currentSystem;
        builder = sh;
        # Reading dep through the castore-FUSE lower is what EIOd in
        # production when input_roots was empty under closure-scoped
        # enforce; here it just proves the dep is reachable.
        args = [ "-c" "''${bb} cat ''${dep}; ''${bb} printf '%s' fod-substituted-parent-payload > $out" ];
      };
    in { inherit dep parent; }
  '';

  # refs-end-to-end: two-stage build where consumer's $out embeds dep's
  # store path as a literal string. The worker's RefScanSink finds the
  # hash part during NAR dump → PutPath sends references=[dep] → PG
  # narinfo."references" is non-empty → GC mark's CTE walks it.
  #
  # dep's output (just "i am the dep payload" text, NO store paths) has
  # refs=[] — same as mkTrivial. Only consumer has a non-empty ref set.
  # This asymmetry is load-bearing: the GC-survival half of the test
  # pins ONLY consumer and asserts dep survives via the reference edge.
  #
  # ''${...}/''' escaping: the inner .nix reads its OWN let-bound
  # busybox/dep, not this evaluation's scope.
  refsDrvFile = pkgs.writeText "lifecycle-refs.nix" ''
    { busybox }:
    let
      sh = "''${busybox}/bin/sh";
      bb = "''${busybox}/bin/busybox";
      dep = derivation {
        name = "rio-refs-dep";
        system = builtins.currentSystem;
        builder = sh;
        args = [ "-c" '''
          ''${bb} mkdir -p $out
          ''${bb} echo "i am the dep payload" > $out/payload
        ''' ];
      };
      consumer = derivation {
        name = "rio-refs-consumer";
        system = builtins.currentSystem;
        builder = sh;
        args = [ "-c" '''
          ''${bb} mkdir -p $out
          # This line embeds dep's FULL /nix/store/HASH-rio-refs-dep
          # into $out/script. RefScanSink (upload.rs) finds the 32-char
          # nixbase32 hash part during the pre-scan NAR dump.
          ''${bb} echo "source path: ''${dep}" > $out/script
          ''${bb} cat ''${dep}/payload >> $out/script
        ''' ];
      };
    in { inherit dep consumer; }
  '';

  # ── testScript prelude: bootstrap + Python helpers ────────────────────
  # Shared by all fragment compositions. start_all + waitReady (~4min on
  # k3s-full) + kubectlHelpers + metric-scrape defs + sshKeySetup + seed.
  # Pyflakes doesn't warn on unused function DEFS (only imports/locals),
  # so sparse splits that don't call every helper are fine.
  prelude = ''
    ${common.assertions}

    ${common.kvmCheck}
    start_all()
    ${fixture.waitReady}

    ${fixture.kubectlHelpers}

    # ── Metrics-scrape helpers ────────────────────────────────────────
    # Scheduler/controller/store pods are minimal images (no sh, no curl).
    # Scrape via the apiserver's pods/proxy subresource — `kubectl get
    # --raw /api/v1/.../pods/{pod}:metrics/proxy/metrics`. Apiserver
    # proxies HTTP to the pod via kubelet. No local port bind, no
    # TIME_WAIT churn, no `sleep 2`.
    #
    # Prior port-forward approach: each sched_metric_wait retry spawned
    # a fresh pf on port 19091. After a long wait (settle-wait took
    # 100s in v18 ≈ 30+ retries), the port was in heavy TIME_WAIT and
    # subsequent calls failed bind for 60s+.
    #
    # NUMERIC ports (9091/9094), not named (`:metrics`): k3s
    # apiserver PANICS (nil-deref in normalizeLocation,
    # upgradeaware.go:173) on named-port proxy. Observed v20.
    # Fresh leader_pod() lookup per scrape — the leader CHANGES
    # across recovery.

    def proxy_url(pod, port, path="metrics"):
        return (
            f"/api/v1/namespaces/${ns}/pods/{pod}:{port}/proxy/{path}"
        )

    def sched_metrics():
        """One-shot scrape of the CURRENT scheduler leader's /metrics."""
        raw = k3s_server.succeed(
            f"k3s kubectl get --raw '{proxy_url(leader_pod(), 9091)}'"
        )
        return parse_prometheus(raw)

    def ctrl_metrics():
        """One-shot scrape of the controller pod's /metrics (port 9094)."""
        pod = kubectl(
            "get pods -l app.kubernetes.io/name=rio-controller "
            "-o jsonpath='{.items[0].metadata.name}'"
        ).strip()
        raw = k3s_server.succeed(
            f"k3s kubectl get --raw '{proxy_url(pod, 9094)}'"
        )
        return parse_prometheus(raw)

    # Shell-inline version for wait_until_succeeds (condition must be
    # shell-evaluable). Single kubectl call per retry — no background
    # process, no cleanup, no port. Retry rate is now limited only by
    # the NixOS test driver's poll interval (~1s) + apiserver RTT.
    def sched_metric_wait(condition, timeout=60):
        """Wait until the leader's /metrics satisfies a bash condition.
        `condition` is a pipe-fragment appended after `... | `."""
        try:
            k3s_server.wait_until_succeeds(
                "leader=$(k3s kubectl -n ${ns} get lease rio-scheduler-leader "
                "  -o jsonpath='{.spec.holderIdentity}') && "
                'test -n "$leader" && '
                "k3s kubectl get --raw "
                '"/api/v1/namespaces/${ns}/pods/$leader:9091/proxy/metrics" '
                f"| {condition}",
                timeout=timeout,
            )
        except Exception:
            # I-056-style per-clause diagnostic: dispatch-stall flakes
            # (builder pod Running but derivations_running stays 0) are
            # invisible from kernel logs alone. Dump scheduler metrics,
            # scheduler logs (executor/dispatch/rejection), and builder
            # logs so the flake names which gate fired.
            k3s_server.execute(
                f"echo '=== DIAG[sched_metric_wait]: timeout={timeout}s, cond={condition!r} ===' >&2; "
                "leader=$(k3s kubectl -n ${ns} get lease rio-scheduler-leader "
                "  -o jsonpath='{.spec.holderIdentity}'); "
                'echo "leader=$leader" >&2; '
                "k3s kubectl get --raw "
                '  "/api/v1/namespaces/${ns}/pods/$leader:9091/proxy/metrics" '
                "  2>/dev/null | grep -E '^rio_scheduler_(workers_active|"
                "derivations_queued|derivations_running|dispatch_rejected)' >&2; "
                "k3s kubectl -n ${nsBuilders} get pods,jobs -o wide >&2 2>&1 || true; "
                'k3s kubectl -n ${ns} logs "$leader" --since=2m '
                "  | grep -iE 'executor|dispatch|reject|intent|heartbeat|worker|recovery' "
                "  | grep -vE '\"level\":\"DEBUG\"' | tail -60 >&2 || true; "
                "for p in $(k3s kubectl -n ${nsBuilders} get pods "
                "  -l rio.build/pool -o name 2>/dev/null); do "
                '  echo "=== builder $p ===" >&2; '
                "  k3s kubectl -n ${nsBuilders} logs $p --since=2m 2>&1 | tail -30 >&2; "
                "done || true"
            )
            raise

    # Delta-based variant for counters that may RESET across leader
    # failover. The absolute `sched_metric_wait` above races whether a
    # post-failover counter has already reached its target by the time
    # we scrape (s3-delta-snapshot-race): snapshotting AFTER the
    # lease-moved gate captures base=1 from the already-recovered new
    # leader on a fast path and waits for >=2 forever. Instead the
    # caller takes a (leader, value) snapshot from a KNOWN leader
    # BEFORE any failover, and the wait branches on whether that
    # leader still holds.
    #
    # `metric_re` is an awk-regex string matching exactly one prom
    # series line (no single-quote, no slash; use [{] / [}] for
    # literal braces — portable across awk variants). Same regex form
    # for both snapshot and wait so they agree on the series. The awk
    # body has NO `exit` after print: succeed() runs under
    # `set -o pipefail`, so awk closing stdin early would SIGPIPE
    # kubectl (exit 141) and fail the command even though the value
    # was extracted.
    def sched_metric_snapshot(metric_re):
        """One-shot (leader, value) capture for delta-based waits.
        Value defaults to 0.0 when the series is absent (fresh
        process, never emitted yet)."""
        snap_leader = leader_pod()
        raw = k3s_server.succeed(
            "k3s kubectl get --raw "
            f"'{proxy_url(snap_leader, 9091)}' "
            f"| awk '/{metric_re}/{{print $2}}'"
        ).strip()
        return (snap_leader, float(raw) if raw else 0.0)

    def sched_metric_wait_delta(snapshot, metric_re, delta=1, timeout=300):
        """Wait until the CURRENT leader's series (matched by
        metric_re) has advanced by >= `delta` from `snapshot`.
        Branches on leader identity: same leader -> cur >= snap+delta;
        leader CHANGED (fresh process, counter reset) -> cur >= delta.
        awk `+0` coerces empty/float prom values for numeric compare."""
        snap_leader, snap_value = snapshot
        try:
            k3s_server.wait_until_succeeds(
                "cur=$(k3s kubectl -n ${ns} get lease rio-scheduler-leader "
                "  -o jsonpath='{.spec.holderIdentity}') && "
                'test -n "$cur" && '
                'v=$(k3s kubectl get --raw '
                '"/api/v1/namespaces/${ns}/pods/$cur:9091/proxy/metrics" '
                f"| awk '/{metric_re}/{{print $2}}') && "
                f'if [ "$cur" = "{snap_leader}" ]; then '
                f'  awk -v c="$v" -v b={snap_value} -v d={delta} '
                "'BEGIN{exit !(c+0 >= b+0+d)}'; "
                "else "
                f'  awk -v c="$v" -v d={delta} '
                "'BEGIN{exit !(c+0 >= d)}'; "
                "fi",
                timeout=timeout,
            )
        except Exception:
            k3s_server.execute(
                "echo '=== DIAG[sched_metric_wait_delta]: "
                f"timeout={timeout}s snap=({snap_leader},{snap_value}) "
                f"delta={delta} ===' >&2; "
                "cur=$(k3s kubectl -n ${ns} get lease rio-scheduler-leader "
                "  -o jsonpath='{.spec.holderIdentity}'); "
                'echo "cur_leader=$cur" >&2; '
                'k3s kubectl get --raw '
                '"/api/v1/namespaces/${ns}/pods/$cur:9091/proxy/metrics" '
                "  2>/dev/null | grep rio_scheduler_recovery_total >&2 || true; "
                'k3s kubectl -n ${ns} logs "$cur" --since=2m '
                "  | grep -iE 'recover|lease|leader' "
                "  | grep -vE '\"level\":\"DEBUG\"' | tail -40 >&2 || true"
            )
            raise

    # NOTE: the stream-era `wait_workers_zero` helper (the
    # heartbeat-timeout-bounded workers_active==0 precondition) was
    # removed at the T-1c.2b corpus re-point. Pull-mode pods never
    # register, so subtests that need a clean-slate precondition wait
    # for the builder pods themselves to be gone (the pod-level wait
    # the pull-mode/ephemeral-pool fragments carry inline).

    # Negative-apply a deliberately-invalid Pool spec. CRD CEL rules
    # (rio-crds/src/pool.rs x_kube validations) are cross-field
    # constraints that fire at kubectl-apply admission.
    # --dry-run=server sends to the apiserver (CEL evaluates) without
    # persisting. fail() asserts non-zero exit; the message-assert
    # proves it failed at the RIGHT rule — not, say, a schema error or
    # the wrong CEL rule. Quoted heredoc (<<'EOF') prevents shell
    # expansion inside the spec body.
    def assert_cel_rejects(name, spec_body, expected_msg, kind="Builder"):
        """spec_body is the YAML body UNDER `spec:` (2-space leading
        indent, no trailing newline on the last line). `kind` fills the
        required spec.kind (Builder/Fetcher); expected_msg is a
        substring of the CEL rule's .message() at rio-crds/src/pool.rs."""
        result = k3s_server.fail(
            "k3s kubectl apply --dry-run=server -f - 2>&1 <<'EOF'\n"
            "apiVersion: rio.build/v1alpha1\n"
            "kind: Pool\n"
            f"metadata:\n  name: {name}\n  namespace: ${nsBuilders}\n"
            f"spec:\n  kind: {kind}\n{spec_body}\n"
            "EOF"
        )
        assert expected_msg in result, (
            f"CEL should reject {name!r} with {expected_msg!r} in the "
            f"message, got: {result!r}. If the apply succeeded or "
            f"failed for a different reason, the CEL rule at "
            f"rio-crds/src/pool.rs isn't in the deployed CRD — "
            f"check `helm template | grep x-kubernetes-validations`."
        )
        print(f"{name}: CEL rejected with {expected_msg!r} ✓")

    # grpcurl against the scheduler's gRPC port (9001) and store (9002).
    # Plaintext gRPC (Cilium WireGuard handles encryption); port-forward
    # is a raw TCP tunnel through the apiserver. `-max-time` bounds the
    # RPC itself; port-forward is killed by trap even if grpcurl hangs.
    def sched_grpc(payload, method):
        """TriggerGC etc. on the scheduler leader. Returns stdout+stderr.
        Carries x-rio-tenant-token (require_tenant gate) AND
        x-rio-service-token (ensure_service_caller gate on
        AdminService). SchedulerService ignores the service token;
        AdminService ignores the tenant token."""
        return pf_exec(leader_pod(), 9001,
            f"${grpcurl} ${grpcurlTls} -max-time 30 "
            f"-H 'x-rio-tenant-token: {tenant_jwt}' "
            f"-H 'x-rio-service-token: {service_token}' "
            f"-protoset ${protoset}/rio.protoset "
            f"-d '{payload}' localhost:__PORT__ {method}")

    def pin_live(out_path, tag):
        """Insert a scheduler_live_pins row for out_path so the GC mark
        phase treats it as a root (seed (e) in gc/mark.rs). Looks up
        store_path_hash via narinfo (PK is the BYTEA hash, not the text
        path). `tag` fills drv_hash — the table's real writer (scheduler
        dispatch) puts a derivation hash there; for test pins it's an
        arbitrary label so unpin/count assertions can scope to rows WE
        inserted, isolated from any scheduler-written rows."""
        psql_k8s(k3s_server,
            f"INSERT INTO scheduler_live_pins (store_path_hash, drv_hash) "
            f"SELECT store_path_hash, '{tag}' FROM narinfo "
            f"WHERE store_path = '{out_path}'"
        )

    def unpin_live(out_path, tag):
        psql_k8s(k3s_server,
            f"DELETE FROM scheduler_live_pins WHERE drv_hash = '{tag}' "
            f"AND store_path_hash = (SELECT store_path_hash FROM narinfo "
            f" WHERE store_path = '{out_path}')"
        )

    def submit_build_grpc(payload: dict, max_time: int = 5) -> str:
        """SubmitBuild via port-forward + grpcurl. Returns buildId.

        `max_time` caps the stream read — build usually won't finish,
        grpcurl exits DeadlineExceeded; ok_nonzero swallows. The build
        is persisted on receipt; stream is observability only. pf_exec
        auto-allocates a fresh port (TIME_WAIT-safe — SubmitBuild calls
        stack within one subtest, e.g. build-timeout submit→retry)."""
        out = pf_exec(leader_pod(), 9001,
            f"${grpcurl} ${grpcurlTls} -max-time {max_time} "
            f"-H 'x-rio-tenant-token: {tenant_jwt}' "
            f"-protoset ${protoset}/rio.protoset "
            f"-d '{json.dumps(payload)}' "
            f"localhost:__PORT__ rio.scheduler.SchedulerService/SubmitBuild",
            ok_nonzero=True)
        return _parse_submit_build_id(out)

    ${common.mkSubmitHelpers "k3s-server"}

    def grpcurl_json_stream(out: str) -> list[dict]:
        """Parse grpcurl's concatenated-JSON output (one pretty-printed
        object per stream message). Returns list of dicts. Empty input →
        empty list. Leading non-JSON (warnings, kubectl port-forward
        chatter from 2>&1) is skipped by seeking to the first `{`;
        inter-object gaps re-seek to the next `{`."""
        dec, objs = json.JSONDecoder(), []
        idx = out.find("{")
        while 0 <= idx < len(out):
            obj, idx = dec.raw_decode(out, idx)
            objs.append(obj)
            idx = out.find("{", idx)
        return objs

    # ── Tenant + JWT for grpcurl-direct + ssh-ng ──────────────────────
    # require_tenant() (r[sched.tenant.authz]) rejects tokenless
    # SchedulerService calls when jwtEnabled. Two callers reach the
    # scheduler from this prelude:
    #   - ssh-ng (build()): gateway parses the SSH key comment as
    #     tenant_name, resolves → UUID, mints x-rio-tenant-token. Empty
    #     comment → no JWT → Unauthenticated. So sshKeySetupFor below
    #     gives the key a non-empty comment naming THIS tenant.
    #   - grpcurl-direct (submit_build_grpc/sched_grpc): we mint the
    #     token here with the same lib/jwt-keys.nix seed the chart
    #     loads. Same tenant UUID so cancel-cgroup-kill's CancelBuild
    #     authorizes against the SubmitBuild it follows.
    # When jwtEnabled=false (prod-parity fixture), the scheduler
    # interceptor is inert (header ignored) and require_tenant returns
    # Ok(None) → tenant_name body fallback resolves the same row.
    #
    # gc_retention_hours=0: gc-sweep + refs-end-to-end backdate narinfo
    # rows past grace and assert exact pathsCollected counts. The
    # default 168h retention would make seed-f (path_tenants) protect
    # those paths (first_referenced_at is recent) → pathsCollected
    # would be 0. With 0h, `first_referenced_at > now()` is always
    # false → seed-f yields nothing for this tenant → GC assertions
    # see the same counts they did pre-tenant-authz.
    tenant_id = psql_k8s(k3s_server,
        "INSERT INTO tenants (tenant_name, gc_retention_hours) "
        "VALUES ('vm-lifecycle', 0) RETURNING tenant_id"
    )
    tenant_jwt = k3s_server.succeed(f"${signJwt} {tenant_id}").strip()
    service_token = k3s_server.succeed(
        "k3s kubectl -n ${ns} get secret rio-service-hmac "
        "-o jsonpath='{.data.service-hmac\\.key}' | ${signServiceToken}"
    ).strip()
    print(f"lifecycle: tenant vm-lifecycle={tenant_id}")

    # ── SSH + seed ────────────────────────────────────────────────────
    # fixture.sshKeySetupFor (NOT common.sshKeySetup): patches the
    # rio-gateway-ssh Secret + rollout-restarts the gateway Deployment.
    # The common.nix version writes /var/lib/rio/gateway/authorized_keys
    # on a systemd host — wrong for a pod.
    ${fixture.sshKeySetupFor "vm-lifecycle"}
    ${common.seedBusybox "k3s-server"}

    # ── Build helper ──────────────────────────────────────────────────
    # client's programs.ssh.extraConfig routes `Host k3s-server` →
    # port 32222, user rio (mkClientNode in common.nix:348-356). So
    # `ssh-ng://k3s-server` hits the gateway NodePort.
    ${common.mkBuildHelperV2 {
      gatewayHost = "k3s-server";
      dumpLogsExpr = ''dump_all_logs([], kube_node=k3s_server, kube_namespace="${ns}")'';
    }}
  '';

  # ── Subtest fragments ─────────────────────────────────────────────────
  # Each fragment is a `with subtest(...)` block + its comment banner,
  # one file per subtest under scenarios/lifecycle/. Fragments are
  # composed by mkTest in the order given by `subtests`. Python variables
  # flow between fragments at module scope (no `with` scoping) — but in
  # the split architecture all fragments are self-contained (gc-sweep
  # builds its own paths; the old `initial` seed-subtest is gone).
  #
  # `scope` is the closure each fragment file sees via `with scope;` —
  # the fixture vars, drv let-bindings, and pkgs/common it interpolates.
  scope = {
    inherit
      pkgs
      common
      ns
      nsStore
      nsBuilders
      nsFetchers
      grpcurl
      grpcurlTls
      protoset
      pinDrv
      recoverySlowDrv
      recoveryDrv
      cancelDrv
      timeoutDrv
      gcVictimDrv
      ephemeralDrv1
      ephemeralDrv2
      pullDrv1
      pullDrv2
      pcPullOk
      pcPullFail
      pcCancelDrv
      pcPreemptDrv
      pcEstabDrv
      pcFetcherFod
      tenantDrv
      rolloutPreDrv
      rolloutPostDrv
      fodSubstitutedDrvFile
      refsDrvFile
      authzDrv
      signJwt
      ;
  };
  fragments = builtins.mapAttrs (_: f: f scope) (common.importDir ./lifecycle);

  mkTest = common.mkFragmentTest {
    scenario = "lifecycle";
    inherit prelude fragments fixture;
    defaultTimeout = 900;
    chains = [ ];
  };
in
{
  # `prelude` exported so scenarios/batch-a.nix (issue #57 1e) can reuse
  # the full k3s bring-up + grpc/metric/tenant helper stanza as the
  # shared bootstrap for its sequential subtest groups.
  inherit prelude fragments mkTest;
}
